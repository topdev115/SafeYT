apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.zaytoona.youtube.safe"
        minSdkVersion 16
        targetSdkVersion 28

        versionName "1.00"    // digits of the *fractional part* shall NOT be > than "9"
        versionCode 56

        // Third party devs:  pls be ethical and change the below keys
        buildConfigField 'String', 'YOUTUBE_API_KEYS_DEBUG_1', '"AIzaSyCUX2IR7_gmRaf"'
        buildConfigField 'String', 'YOUTUBE_API_KEYS_DEBUG_2', '"u2hFJ1NewGFruLQrP6ag"'
        buildConfigField 'String[]', 'YOUTUBE_API_KEYS_1',
                '{' +
                        '"AIzaSyCAJzLACSDR1",' +
                        '"AIzaSyCgbjzJgHrD",' +
                        '"AIzaSyAnq8FimIaT8DC",' +
                        '"AIzaSyARZSqSlY5V9DQ3",' +
                        '"AIzaSyA-ctUv3_ORV0eJ-",' +
                        '"AIzaSyDi3HiFQ3OKC-fQC",' +
                        '"AIzaSyBrJpGqmYr6_T",' +
                        '"AIzaSyAr5iQYBJWdKr",' +
                        '"AIzaSyDY3vpgIFHI"' +
                        '}'

        buildConfigField 'String[]', 'YOUTUBE_API_KEYS_2',
                '{' +
                        '"DGzX3jcHMrRqOcDIHF4zhg",' +
                        '"aSxmRI_VF-rGn45RAN5Kn-Q",' +
                        '"g4e_YTawQE146mndJX2E",' +
                        '"GUYiUh2KTJSoWaLIgcY",' +
                        '"ajuwWd1oB3dyuqr9Q0",' +
                        '"9FtCD9CQRq90gerpro",' +
                        '"sjWulFJWxeBMJUpEUxEko",' +
                        '"r_Rf7Ga3cNql_hoNmSIcs",' +
                        '"85srfrZ1xPtaiH0SkHIP9s4"' +
                        '}'

        buildConfigField 'String', 'SAFETOONS_WEBSITE', '"http://www.zaytoona.ie/products/safetoons"'
        buildConfigField 'String', 'SAFETOONS_WEBSITE_PRIVACY', '"http://www.zaytoona.ie/products/safetoons/privacy/"'
        //buildConfigField 'String', 'SAFETOONS_WEBSITE_CREDITS', '"https://Safetoons-app.com/credits.html"'
        buildConfigField 'String', 'SAFETOONS_UPDATES_URL', '"https://api.github.com/repos/sgheith/updates/releases/latest"'
        vectorDrawables.useSupportLibrary true
        multiDexEnabled true    // as we have over 65536 lines of code...

        // This is to avoid warning in the english files that are
        // not translated to other than supported languages below (just arabic for now)
        resConfigs "ar"
    }

    // Get SHA1
    //Signing configurations for build variants "release"
    // In Java bin run below to het release SHA1
    // keytool -list -v -keystore "C:\Git Repo\My Products\SafeYouTube\SafeYouTube App\keystore.jks" -alias alphakey


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "safetoons"
    productFlavors {
        // 100% Open-Source Edition
        oss {
            applicationId "com.zaytoona.youtube.safe.oss"
            resValue "string", "app_name", "Safetoons"
            dimension "safetoons"
        }
        // 99% Open-source edition:  uses the YouTube Player Jar [i.e. proprietary module - optional usage]
        extra {
            applicationId "com.zaytoona.youtube.safe.extra"
            resValue "string", "app_name", "Safetoons"
            dimension "safetoons"
        }
    }
    // Generate APK filenames as follows:  <App Name>-<Flavor><Version>
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "Safetoons-${variant.versionName}.apk"
        }
    }
    // Ignore missing translations
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'

    // YouTube modules
    implementation 'com.google.http-client:google-http-client-android:1.23.0'
    implementation 'com.google.apis:google-api-services-youtube:v3-rev187-1.23.0'
    implementation 'com.github.TeamNewPipe:NewPipeExtractor:560c648e92476cc220a036f1668c4335b98ceece'

    // other modules
    implementation group: 'org.ocpsoft.prettytime', name: 'prettytime', version: '4.0.1.Final'
    implementation 'com.github.moraisigor:slidingdrawer:1.6.7'
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation group: 'joda-time', name: 'joda-time', version: '2.9.9'
    implementation 'com.afollestad.material-dialogs:core:0.9.5.0'
    implementation 'com.github.angads25:filepicker:1.1.1'
    implementation 'com.google.android.exoplayer:exoplayer:2.8.2'
    implementation 'com.klinkerapps:link_builder:1.6.1'
    implementation(group: 'com.optimaize.languagedetector', name: 'language-detector', version: '0.6') {
        exclude group: 'com.google.guava', module: 'guava'
    }
    implementation 'com.mikepenz:actionitembadge:3.3.2@aar'
    implementation "com.mikepenz:iconics-core:3.0.3@aar"

    // Android support modules
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-v13:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:multidex:1.0.3' // as we have over 65536 lines of code...

    // Country code
    implementation('com.hbb20:ccp:2.2.4', {
        exclude group: 'com.android.support'
    })

    // proprietary module
    extraImplementation files('libs/YouTubeAndroidPlayerApi.jar')
    implementation 'com.google.firebase:firebase-core:16.0.1'
    implementation 'com.google.firebase:firebase-database:16.0.1'
    implementation 'com.google.firebase:firebase-storage:16.0.1'
    implementation 'com.google.firebase:firebase-auth:16.0.1'
    implementation 'com.google.firebase:firebase-functions:16.0.1'
    implementation 'com.google.firebase:firebase-messaging:17.1.0'

    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.9'

    com.google.gms.googleservices.GoogleServicesPlugin.config.disableVersionCheck = true

    //Lollipin
    //implementation('com.github.omadahealth:lollipin:2.1.0@aar') {
    //transitive = true
    //}

    // Pin code library
    implementation project(":pincodelibrary")

    // Multi-select expandable Recycler View (Firebase)
    implementation project(':expandablecheckrecyclerview')
    implementation project(':expandablerecyclerview')

    //Android Rate library
    implementation project(':AndroidRate')
}
